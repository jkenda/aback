include "core/stack.ab"

macro N is 1000000 end

mem   primes     int N end
mem   primes.len int 1 end

(is the number a prime number?)
macro is_prime?
    int -> bool
is
take cand in
    0 true while
        peek i prime in
            &&  prime
                <= * dup [] primes i cand
        end
    do
        take i prime in
            + 1 i
            if = 0 % cand [] primes i then
                false
            else prime end
        end
    end drop
end end

(number of primes from 1 to limit)
macro nprimes
    int ->
is
take limit in
    5 while < [] primes.len 0 limit do
        take cand in
            if is_prime? cand then
                [] primes.len 0 ; (len --)
                := [] primes over cand ; (len --)
                := [] primes.len 0 (--)
                   + 1
            end
            + 2 cand
        end
    end
    drop
end end

(initialize the array of primes)
:= [] primes 0 2 ;
:= [] primes 1 3 ;
:= [] primes.len 0 2 ;

(search the first N primes)
nprimes N ;

0 while < over [] primes.len 0 do
    puti [] primes dup ; putc ' ' ;
    + 1
end drop ;
putc '\n'
